apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "android.bignerdranch.my_application"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug" // Application ID - имя приложения для дебага
            manifestPlaceholders = [app_name: "DebugAlkhimov"]
        }

        qa { //Quality assuruanse - название отдела тестирования
            initWith debug
            applicationIdSuffix ".test"
            manifestPlaceholders = [app_name: "QaAlkhimov"]
        }

        release {
            signingConfig signingConfigs.debug //Релизная сборка будет подписываться дебажной подписью
            minifyEnabled false
            proguardFiles getDefaultProguardFile ('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [app_name: "ReleaseAlkhimov"]
        }
    }

    flavorDimensions "type", "brand" //Две группы: тип и бренд 


    productFlavors {
        demo {
            dimension "type"
            applicationIdSuffix ".demo"
        }

        full {
            dimension "type"
            applicationIdSuffix ".full"
        }

        cola {
            dimension "brand"
            applicationIdSuffix ".cola"
            minSdkVersion 28
        }

        pepsi {
            dimension "brand"
            applicationIdSuffix ".papsi"
            versionCode = android.defaultConfig.versionCode + 100
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
